prototype(Carbon.Link:Link) < prototype(Neos.Fusion:Tag) {
    node = null
    link = ${this.node ? 'node://' + this.node.identifier : link ? link : false}
    backendLink = false
    additionalClass = null
    title = null
    defaultTagName = 'span'
    renderDefaultTagIfNoLink = false
    // Make sure that we have a string
    additionalClass.@process {
      convertToString = ${Type.isArray(value) ? Array.join(Carbon.Array.filter(value), ' ') : String.toString(value)}
      trimString = ${String.trim(value)}
    }

    @context {
        link = ${this.link}
        backendLink = ${this.backendLink}
        additionalClass = ${this.additionalClass}
        defaultTagName = ${this.defaultTagName}
        renderDefaultTagIfNoLink = ${this.renderDefaultTagIfNoLink}
        title = ${this.title}
    }
    valid = Carbon.Link:Valid

    @if.hasLink = ${this.renderDefaultTagIfNoLink || (this.link && this.valid) ? true : false}

    tagName = Carbon.Link:TagName {
        defaultTagName = ${defaultTagName}
    }
    attributes = Carbon.Link:Attributes {
        title = ${title}
    }
    content = Carbon.Link:Content
}
